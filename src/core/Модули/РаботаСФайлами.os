// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

#Использовать yadisk
#Использовать 1commands

Перем Лог;           // - Объект    - объект записи лога приложения

#Область ПрограммныйИнтерфейс

// Функция, выполняет копирование/перемещение указанных файлов с использованием команд системы (xcopy)
//
// Параметры:
//   Источник           - Строка       - копируемые файлы
//   Приемник           - Строка       - назначение копирования, каталог или файл
//   Перезаписывать     - Булево       - перезаписывать существующие файлы
//   Перемещение        - Булево       - выполнить перемещение файлов (удалить источник после копирования)
//   ТолькоСегодня      - Булево       - копирование файлов, измененных не ранее текущей даты (параметр /D для xcopy)
//
Процедура КомандаСистемыКопироватьФайл(Источник,
                                       Приемник,
                                       Перезаписывать = Истина,
                                       Перемещение = Ложь,
                                       ТолькоСегодня = Ложь) Экспорт

	КомандаРК = Новый Команда;
	
	КомандаРК.УстановитьКоманду("xcopy");
	КомандаРК.ДобавитьПараметр(Источник);
	КомандаРК.ДобавитьПараметр(Приемник);
	Если Перезаписывать Тогда
		КомандаРК.ДобавитьПараметр("/Y");
	КонецЕсли;
	КомандаРК.ДобавитьПараметр("/Z");
	КомандаРК.ДобавитьПараметр("/V");
	КомандаРК.ДобавитьПараметр("/J");
	Если ТолькоСегодня Тогда
		лТекДата = ТекущаяДата();
		лФорматированнаяДата = Строка(Формат(лТекДата, "ДФ=MM-dd-yyyy"));
		КомандаРК.ДобавитьПараметр("/D:" + лФорматированнаяДата);
	КонецЕсли;

	КомандаРК.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	КомандаРК.ПоказыватьВыводНемедленно(Ложь);
	
	КодВозврата = КомандаРК.Исполнить();

	ОписаниеРезультата = КомандаРК.ПолучитьВывод();
	
	Если Не ПустаяСтрока(ОписаниеРезультата) Тогда
		Лог.Информация("Вывод команды копирования: " + ОписаниеРезультата);
	КонецЕсли;
	
	Если НЕ КодВозврата = 0 Тогда
		ТекстОшибки = СтрШаблон("Ошибка копирования файла ""%1"" -> ""%2"", код возврата %3: %4%5",
		                        Источник,
		                        Приемник,
		                        КодВозврата,
		                        Символы.ПС,
		                        ОписаниеРезультата);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Если Перемещение Тогда
		КомандаСистемыУдалитьФайл(Источник);
	КонецЕсли;

КонецПроцедуры // КомандаСистемыКопироватьФайл()

// Функция, выполняет удаление указанных файлов с использованием команды системы (del)
//   
// Параметры:
//   ПутьКФайлу             - Строка         - путь к удаляемому файлы
//   ИсключениеПриОшибке    - Строка         - Истина - вызывать исключение при ошибке удаления
//
Процедура КомандаСистемыУдалитьФайл(ПутьКФайлу, ИсключениеПриОшибке = Ложь) Экспорт

	КомандаРК = Новый Команда;
	
	КомандаРК.УстановитьКоманду("del");
	КомандаРК.ДобавитьПараметр("/F ");
	КомандаРК.ДобавитьПараметр("/Q ");
	КомандаРК.ДобавитьПараметр(ПутьКФайлу);

	КомандаРК.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	КомандаРК.ПоказыватьВыводНемедленно(Ложь);
	
	КодВозврата = КомандаРК.Исполнить();

	ОписаниеРезультата = КомандаРК.ПолучитьВывод();
	
	Если Не ПустаяСтрока(ОписаниеРезультата) Тогда
		Лог.Информация("Вывод команды удаления: " + ОписаниеРезультата);
	КонецЕсли;

	Если НЕ КодВозврата = 0 Тогда
		ТекстОшибки = СтрШаблон("Ошибка удаления файла ""%1"", код возврата %2: %3%4",
		                        ПутьКФайлу,
		                        КодВозврата,
		                        Символы.ПС,
		                        ОписаниеРезультата);
		Если ИсключениеПриОшибке Тогда
			ВызватьИсключение ТекстОшибки;
		Иначе
			Лог.Ошибка(ТекстОшибки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // КомандаСистемыУдалитьФайл()

// Функция читает список файлов из файла
//
// Параметры:
//   ПутьКСписку                    - Строка    - путь к файлу со списком файлов архива
//   ДобавитьПутьКИсходномуФайлу    - Строка    - Истина - при чтении добавлять к результату
//                                                путь к исходному файлу списка
//   ДобавитьИсходныйФайл           - Строка    - Истина - добавить исходный файл в список
//
// Возвращаемое значение:
//   Массив из Строка    - список файлов архива
//
Функция ПрочитатьСписокФайлов(ПутьКСписку, ДобавитьПутьКИсходномуФайлу = Ложь, ДобавитьИсходныйФайл = Ложь) Экспорт

	ДанныеИсхФайла = Новый Файл(ПутьКСписку);

	МассивФайловЧастей = Новый Массив();
	
	ЧтениеСписка = Новый ЧтениеТекста(ПутьКСписку, КодировкаТекста.UTF8);
	СтрокаСписка = ЧтениеСписка.ПрочитатьСтроку();
	Пока СтрокаСписка <> Неопределено Цикл
		Если ЗначениеЗаполнено(СокрЛП(СтрокаСписка)) Тогда
			Если ДобавитьПутьКИсходномуФайлу Тогда
				МассивФайловЧастей.Добавить(ДанныеИсхФайла.Путь + СтрокаСписка);
			Иначе
				МассивФайловЧастей.Добавить(ДанныеИсхФайла.Путь + СтрокаСписка);
			КонецЕсли;
		КонецЕсли;
		
		СтрокаСписка = ЧтениеСписка.ПрочитатьСтроку();
	КонецЦикла;
		
	ЧтениеСписка.Закрыть();

	Если ДобавитьИсходныйФайл Тогда
		МассивФайловЧастей.Добавить(?(ДобавитьПутьКИсходномуФайлу, ДанныеИсхФайла.ПолноеИмя, ДанныеИсхФайла.Имя));
	КонецЕсли;

	Возврат МассивФайловЧастей;

КонецФункции // ПрочитатьСписокФайлов()

// Выполняет архиваци указанного файла с разбитием на части указанного размера
//   
// Параметры:
//   ПутьКФайлу          - Строка    - путь к файлу, который будет архивироваться
//   ИмяАрхива           - Строка    - имя файла-архива
//   ИмяСпискаФайлов     - Строка    - имя файла-списка (содержащего все чати архива)
//   РазмерТома          - Строка    - размер части {<g>, <m>, <b>} (по умолчанию 50m)
//   РассчитыватьХеши    - Булево    - Истина - будут расчитаны хэш-суммы файлов архива
//   ИмяФайлаХэшей       - Строка    - имя файла списка хэш-сумм
//   СтепеньСжатия       - Число     - уровень сжатия частей архива {0 - 9} (по умолчанию 0 - не сжимать)
//   УдалитьИсточник     - Булево    - Истина - после архивации исходный файл будет удален
//
// Возвращаемое значение:
//   Число    - количество файлов архива
//
Функция ЗапаковатьВАрхив(Знач ПутьКФайлу,
                         Знач ИмяАрхива,
                         Знач ИмяСпискаФайлов,
                         Знач РазмерТома = Неопределено,
                         Знач РассчитыватьХеши = Ложь,
                         Знач ИмяФайлаХэшей = Неопределено,
                         Знач СтепеньСжатия = 0,
                         Знач УдалитьИсточник = Ложь) Экспорт

	ПутьКАрхиватору = НайтиАрхиватор();
	
	Если НЕ ЗначениеЗаполнено(ПутьКАрхиватору) Тогда
		ТекстОшибки = СтрШаблон("Ошибка архивации файла ""%1"": архиватор (7-Zip) не найден",
		                        ПутьКФайлу);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	ДанныеИсхФайла = Новый Файл(ПутьКФайлу);

	Если НЕ ЗначениеЗаполнено(ИмяАрхива) Тогда
		ИмяАрхива = ДанныеИсхФайла.ИмяБезРасширения + ".7z";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ИмяСпискаФайлов) Тогда
		ИмяСпискаФайлов = ОбъединитьПути(ДанныеИсхФайла.Путь, ДанныеИсхФайла.ИмяБезРасширения + ".split");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ИмяФайлаХэшей) Тогда
		ИмяФайлаХэшей = ОбъединитьПути(ДанныеИсхФайла.Путь, ДанныеИсхФайла.ИмяБезРасширения + ".hash");
	КонецЕсли;

	КомандаРК = Новый Команда();
	
	КомандаРК.УстановитьКоманду(ПутьКАрхиватору);
	КомандаРК.ДобавитьПараметр("a");
	КомандаРК.ДобавитьПараметр(ИмяАрхива);
	КомандаРК.ДобавитьПараметр(ПутьКФайлу);
	КомандаРК.ДобавитьПараметр("-t7z");

	Если ЗначениеЗаполнено(РазмерТома) Тогда
		КомандаРК.ДобавитьПараметр(СтрШаблон("-v%1", РазмерТома));
	Иначе
		КомандаРК.ДобавитьПараметр("-v50m");
	КонецЕсли;

	Если ЗначениеЗаполнено(СтепеньСжатия) Тогда
		КомандаРК.ДобавитьПараметр(СтрШаблон("-mx%1", СтепеньСжатия));
	Иначе
		КомандаРК.ДобавитьПараметр("-mx0");
	КонецЕсли;

	КомандаРК.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	КомандаРК.ПоказыватьВыводНемедленно(Ложь);
	
	КодВозврата = КомандаРК.Исполнить();

	ОписаниеРезультата = КомандаРК.ПолучитьВывод();

	Если КодВозврата = 0 Тогда
		Если УдалитьИсточник Тогда
			КомандаСистемыУдалитьФайл(ПутьКФайлу);
		КонецЕсли;
		Возврат СоздатьСписокФайлов(ИмяАрхива, ДанныеИсхФайла.Путь, ИмяСпискаФайлов, РассчитыватьХеши, ИмяФайлаХэшей);
	Иначе
		ТекстОшибки = СтрШаблон("Ошибка архивации файла ""%1"", код возврата %2:%3%4",
		                        ПутьКФайлу,
		                        КодВозврата,
		                        Символы.ПС,
		                        ОписаниеРезультата);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

КонецФункции // ЗапаковатьВАрхив()

// Процедура, выполняет распаковку архива
//   
// Параметры:
//   ПутьКАрхиву         - Строка    - путь к файлу архива, который будет распаковываться
//   ЭтоСписокФайлов     - Булево    - Истина - передан список файлов;
//                                     Ложь - передан первый том архива
//   УдалитьИсточник     - Булево    - Истина - после распаковки исходный файл будет удален
//
Процедура РаспаковатьАрхив(Знач ПутьКАрхиву, Знач ЭтоСписокФайлов = Ложь, Знач УдалитьИсточник = Ложь) Экспорт

	ПутьКАрхиватору = НайтиАрхиватор();

	Если НЕ ЗначениеЗаполнено(ПутьКАрхиватору) Тогда
		ТекстОшибки = СтрШаблон("Ошибка распаковки архива ""%1"": архиватор (7-Zip) не найден",
		                        ПутьКАрхиву);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	ДанныеИсхФайла = Новый Файл(ПутьКАрхиву);

	ИмяФайлаОшибокАрхивации = ДанныеИсхФайла.Путь + "7z_error_messages.txt";

	МассивФайловЧастей = Новый Массив();

	Если ЭтоСписокФайлов Тогда
		МассивФайловЧастей = ПрочитатьСписокФайлов(ПутьКАрхиву, Истина, Истина);
		ПерваяЧастьАрхива = МассивФайловЧастей[0];
	Иначе
		ПерваяЧастьАрхива = ПутьКАрхиву;
		МассивФайловЧастей = НайтиФайлы(ДанныеИсхФайла.Путь, ДанныеИсхФайла.ИмяБезРасширения + ".???", Ложь);
	КонецЕсли;

	Лог.Отладка("Всего частей: " + МассивФайловЧастей.Количество());

	КомандаРК = Новый Команда();
	
	КомандаРК.УстановитьКоманду(ПутьКАрхиватору);
	КомандаРК.ДобавитьПараметр("x");
	КомандаРК.ДобавитьПараметр("-aoa");
	КомандаРК.ДобавитьПараметр("-y");
	КомандаРК.ДобавитьПараметр(ПерваяЧастьАрхива);

	КомандаРК.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	КомандаРК.ПоказыватьВыводНемедленно(Ложь);
	
	КодВозврата = КомандаРК.Исполнить();

	ОписаниеРезультата = КомандаРК.ПолучитьВывод();

	Если КодВозврата = 0 Тогда
		Если УдалитьИсточник Тогда
			Для Каждого ФайлЧасти Из МассивФайловЧастей Цикл
				Если ТипЗнч(ФайлЧасти) = Тип("Файл") Тогда
					КомандаСистемыУдалитьФайл(ФайлЧасти.ПолноеИмя);
				Иначе
					КомандаСистемыУдалитьФайл(ФайлЧасти);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ТекстОшибки = СтрШаблон("Ошибка распаковки архива ""%1"", код возврата %2:%3%4",
		                        ПерваяЧастьАрхива,
		                        КодВозврата,
		                        Символы.ПС,
		                        ОписаниеРезультата);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

КонецПроцедуры // РаспаковатьАрхив()

// Создает папку на Я-Диске
//
// Параметры:
//   ЯДиск          - ЯндексДиск    - объект ЯндексДиск для работы с yandex-диском
//   ЦелевойПуть    - ЯндексДиск    - путь на yandex-диске к создаваемому каталогу
//
// Возвращаемое значение:
//   Строка    - Созданный путь
//
Функция СоздатьПапкуНаЯДиске(ЯДиск, Знач ЦелевойПуть) Экспорт

	КаталогНайден = Ложь;
	Попытка
		СвойстваПапки = ЯДиск.ПолучитьСвойстваРесурса(ЦелевойПуть);
		КаталогНайден = Истина;
	Исключение
		СвойстваПапки = Новый Структура("type", "dir");
	КонецПопытки;

	Если СвойстваПапки["type"] <> "dir" Тогда
		ТекстОшибки = СтрШаблон("Ошибка при создании папки  Яндекс-Диска: %1", ЦелевойПуть);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	ТекущийПуть = "";
	Если НЕ КаталогНайден Тогда
		Попытка
			ЯДиск.СоздатьПапку(ЦелевойПуть);
		Исключение
			ТекстОшибки = СтрШаблон("Ошибка при создании папки %1: %2%3",
			                        ЦелевойПуть,
			                        Символы.ПС,
			                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение ТекстОшибки;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ТекущийПуть;

КонецФункции // СоздатьПапкуНаЯДиске()

// Функция отправки файла на Yandex-Диск
//
// Параметры:
//   ЯДиск          - ЯндексДиск    - объект ЯндексДиск для работы с yandex-диском
//   Каталог        - Строка        - расположение загружаемого файла
//   ИмяФайла       - Булево        - имя загружаемого файла
//   ЦелевойПуть    - ЯндексДиск    - путь на yandex-диске, куда будет загружен файл
//   Перезаписывать - Булево        - перезаписать файл на Яндекс-диске при загрузке
//
Процедура ОтправитьФайлНаЯДиск(ЯДиск, Знач Каталог, Знач ИмяФайла, Знач ЦелевойПуть, Перезаписывать = Ложь) Экспорт
	
	СвойстваДиска = ЯДиск.ПолучитьСвойстваДиска();
	Лог.Отладка("Всего доступно %1 байт", СвойстваДиска.total_space);
	Лог.Отладка("Из них занято %1 байт", СвойстваДиска.used_space);
	
	СвободноМеста = СвойстваДиска.total_space - СвойстваДиска.used_space;
	Лог.Отладка("Копируемый файл: каталог %1, имя файла %2", Каталог, ИмяФайла);
	ИсходныйФайл = Новый Файл(Каталог + "\" + ИмяФайла);
	ИмяЗагружаемогоФайла = ЦелевойПуть + "/" + ИсходныйФайл.Имя;
	
	Если СвободноМеста < ИсходныйФайл.Размер() Тогда
		ТекстОшибки = СтрШаблон("Недостаточно места на ЯДиске для копирования файла %1: есть %2, надо %3",
		                        ИсходныйФайл.Имя,
		                        СвободноМеста,
		                        ИсходныйФайл.Размер());
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Попытка
		ЯДиск.ЗагрузитьНаДиск(ИсходныйФайл.ПолноеИмя, ИмяЗагружаемогоФайла, Перезаписывать);
		Лог.Информация("Файл загружен %1", ИсходныйФайл.Имя);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка загрузки файла %1 в %2:%3%4",
		                        ИсходныйФайл.Имя,
		                        ИмяЗагружаемогоФайла,
		                        Символы.ПС,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

	Попытка
		ЯДиск.ПолучитьСвойстваРесурса(ИмяЗагружаемогоФайла);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка при получении свойств файла %1:%2%3",
		                        ИмяЗагружаемогоФайла,
		                        Символы.ПС,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
КонецПроцедуры // ОтправитьФайлНаЯДиск()

// Функция получения файла из Yandex-Диска
//
// Параметры:
//   ЯДиск              - ЯндексДиск    - объект для работы с yandex-диском
//   ПутьНаДиске        - Строка        - расположение файла на yandex-диске
//   ЦелевойПуть        - Строка        - путь, куда будет загружен файл
//   УдалитьИсточник    - Булево        - Истина - удалить файл после загрузки
//
// Возвращаемое значение:
//   Число - код возврата команды
//
Функция ПолучитьФайлИзЯДиска(ЯДиск, Знач ПутьНаДиске, Знач ЦелевойПуть, УдалитьИсточник = Ложь) Экспорт
	
	ПутьКСкачанномуФайлу = "";
	
	Попытка
		ПутьКСкачанномуФайлу = ЯДиск.СкачатьФайлСДиска(ЦелевойПуть, ПутьНаДиске, Истина);

		Лог.Информация("Файл получен %1", ПутьКСкачанномуФайлу);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка получения файла %1: %2",
		                        ПутьНаДиске,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

	Если УдалитьИсточник Тогда
		ЯДиск.Удалить(ПутьНаДиске, Истина);
		СвойстваДиска = ЯДиск.ПолучитьСвойстваДиска();
		Лог.Информация(СтрШаблон("Удален файл на Yandex-Диск %1", ПутьНаДиске));
		Лог.Отладка(СтрШаблон("Всего доступно %1 байт", СвойстваДиска.total_space));
		Лог.Отладка(СтрШаблон("Из них занято %1 байт", СвойстваДиска.used_space));
	КонецЕсли;
	
	Возврат ПутьКСкачанномуФайлу;

КонецФункции // ПолучитьФайлИзЯДиска()

// Процедура подключает указанный сетевой диск
//
// Параметры:
//   ИмяУстройства         - Строка    - имя (буква) подключаемого диска
//   ИмяРесурса            - Строка    - сетевой путь к подключаемому ресурсу
//   Пользователь          - Строка    - пользователь от имени которого выполняется подключение
//   ПарольПользователя    - Строка    - пароль пользователя от имени которого выполняется подключение
//
Процедура ПодключитьДиск(ИмяУстройства, ИмяРесурса, Пользователь, ПарольПользователя) Экспорт

	КомандаРК = Новый Команда;
	
	КомандаРК.УстановитьКоманду("net");
	КомандаРК.ДобавитьПараметр("use");
	КомандаРК.ДобавитьПараметр(ИмяУстройства);
	КомандаРК.ДобавитьПараметр(ИмяРесурса);
	КомандаРК.ДобавитьПараметр(ПарольПользователя);
	КомандаРК.ДобавитьПараметр("/USER:" + Пользователь);

	КомандаРК.УстановитьИсполнениеЧерезКомандыСистемы( Ложь );
	КомандаРК.ПоказыватьВыводНемедленно( Ложь );
	
	КодВозврата = КомандаРК.Исполнить();

	ОписаниеРезультата = КомандаРК.ПолучитьВывод();
	
	Если КодВозврата = 0 Тогда
		Лог.Информация("Подключен сетевой ресурс ""%1"" к устройству ""%2"": %3",
		               ИмяРесурса,
		               ИмяУстройства,
		               ОписаниеРезультата);
	Иначе
		ВызватьИсключение СтрШаблон("Ошибка ошибка подключения ресурса ""%1"" к устройству ""%2"", код ошибки %3: %4%5",
		                            ИмяРесурса,
		                            ИмяУстройства,
		                            КодВозврата,
		                            Символы.ПС,
		                            ОписаниеРезультата);
	КонецЕсли;
	
КонецПроцедуры // ПодключитьДиск()

// Процедура отключает указанный сетевой диск
//   
// Параметры:
//   ИмяУстройства    - Строка    - имя (буква) отключаемого диска
//
Процедура ОтключитьДиск(ИмяУстройства) Экспорт

	КомандаРК = Новый Команда;
	
	КомандаРК.УстановитьКоманду("net");
	КомандаРК.ДобавитьПараметр("use");
	КомандаРК.ДобавитьПараметр(ИмяУстройства);
	КомандаРК.ДобавитьПараметр("/DELETE");

	КомандаРК.УстановитьИсполнениеЧерезКомандыСистемы( Ложь );
	КомандаРК.ПоказыватьВыводНемедленно( Ложь );
	
	КодВозврата = КомандаРК.Исполнить();

	ОписаниеРезультата = КомандаРК.ПолучитьВывод();
	
	Если КодВозврата = 0 Тогда
		Лог.Информация("Отключено устройство ""%1"": %2",
		               ИмяУстройства,
		               ОписаниеРезультата);
	Иначе
		ВызватьИсключение СтрШаблон("Ошибка ошибка отключения устройства ""%1"", код ошибки %2: %3%4",
		                            ИмяУстройства,
		                            КодВозврата,
		                            Символы.ПС,
		                            ОписаниеРезультата);
	КонецЕсли;
	
КонецПроцедуры // ОтключитьДиск()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Функция - ищет и возвращает путь к архиватору 7-zip с использованием команды where/whereis
//
// Возвращаемое значение:
//  Строка   - путь к исполняемому файлу архиватора 7-zip
//
Функция НайтиАрхиваторКомандойПоиска()

	ЭтоWindows = ПараметрыСистемы.ЭтоWindows();
	НайденныйПуть = Неопределено;

	Команда = Новый Команда();

	Если ЭтоWindows Тогда
		Команда.УстановитьКоманду("where");
	Иначе
		Команда.УстановитьКоманду("whereis");
	КонецЕсли;
	Команда.ДобавитьПараметр("7z");

	КодВозврата = Команда.Исполнить();
	ВыводКоманды = Команда.ПолучитьВывод();

	Если КодВозврата = 0 Тогда
		НайденныйПуть = ВыводКоманды;
		Если НЕ ЭтоWindows Тогда
			НайденныйПуть = СтрЗаменить(НайденныйПуть, "7z:", ""); 
			ЧастиСтроки = СтрРазделить(СокрЛП(НайденныйПуть), " ");
			НайденныйПуть = ЧастиСтроки[0];

			Если ПустаяСтрока(НайденныйПуть) Тогда
				НайденныйПуть = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат НайденныйПуть;

КонецФункции // НайтиАрхиваторКомандойПоиска()

// Функция - ищет и возвращает путь к архиватору 7-zip в стандартных каталогах установки программ
//
// Возвращаемое значение:
//  Строка   - путь к исполняемому файлу архиватора 7-zip
//
Функция НайтиАрхиваторВКаталогахПрограмм()

	КаталогиПрограмм = Новый Массив();

	Если ПараметрыСистемы.ЭтоWindows() Тогда
		КаталогиПрограмм.Добавить(ПолучитьПеременнуюСреды("ProgramFiles"));
		КаталогиПрограмм.Добавить(ПолучитьПеременнуюСреды("ProgramFiles(x86)"));
		ИмяИсполняемогоФайла = "7z.exe";
	Иначе
		КаталогиПрограмм.Добавить("/usr/bin");
		КаталогиПрограмм.Добавить("/usr/local/bin");
		ИмяИсполняемогоФайла = "7z";
	КонецЕсли;

	НайденныйПуть = Неопределено;

	Для Каждого ТекКаталог Из КаталогиПрограмм Цикл
		Массив7ZIP = НайтиФайлы(ТекКаталог, ИмяИсполняемогоФайла, True);
		Если Массив7ZIP.Количество() > 0 Тогда
			НайденныйПуть = Массив7ZIP[0].ПолноеИмя;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат НайденныйПуть;

КонецФункции // НайтиАрхиваторВКаталогахПрограмм()

// Функция - ищет и возвращает путь к архиватору 7-zip
//
// Возвращаемое значение:
//  Строка   - путь к исполняемому файлу архиватора 7-zip
//
Функция НайтиАрхиватор()

	НайденныйПуть = НайтиАрхиваторКомандойПоиска();

	Если НЕ ЗначениеЗаполнено(НайденныйПуть) Тогда
		НайденныйПуть = НайтиАрхиваторВКаталогахПрограмм();
	КонецЕсли;

	Возврат НайденныйПуть;

КонецФункции // НайтиАрхиватор()

// Функция создает файл-список файлов архива и возвращает количество
//   
// Параметры:
//   ИмяАрхива           - Строка    - имя файла архива
//   КаталогАрхива       - Строка    - путь к каталогу с файлами архива
//   ИмяСпискаФайлов     - Строка    - имя файла-списка файлов архива
//   РассчитыватьХеши    - Булево    - Истина - будут расчитаны хэш-суммы файлов архива
//   ИмяФайлаХэшей       - Строка    - имя файла-списка хэшей файлов архива
//
// Возвращаемое значение:
//   Число - количество файлов архива
//
Функция СоздатьСписокФайлов(ИмяАрхива, КаталогАрхива, ИмяСпискаФайлов, РассчитыватьХеши, ИмяФайлаХэшей)

	МассивФайловЧастей = НайтиФайлы(КаталогАрхива, ИмяАрхива + ".???", Ложь);
	Лог.Отладка("Всего частей: " + МассивФайловЧастей.Количество());

	ЗаписьСписка = Новый ЗаписьТекста(ИмяСпискаФайлов, КодировкаТекста.UTF8, , Ложь);

	Если РассчитыватьХеши Тогда
		ЗаписьХешей = Новый ЗаписьТекста(ИмяФайлаХэшей, КодировкаТекста.UTF8, , Ложь);
		РасчетХешей = Новый ХешированиеДанных(ХешФункция.MD5);
	КонецЕсли;

	Для каждого ФайлЧасти Из МассивФайловЧастей Цикл
		ЗаписьСписка.ЗаписатьСтроку(ФайлЧасти.Имя);

		Если РассчитыватьХеши Тогда
			РасчетХешей.ДобавитьФайл(ФайлЧасти.ПолноеИмя);
			ЗаписьХешей.ЗаписатьСтроку(СтрШаблон("%1 %2", ФайлЧасти.Имя, РасчетХешей.ХешСуммаСтрокой));
			РасчетХешей.Очистить();
		КонецЕсли;

	КонецЦикла;
	ЗаписьСписка.Закрыть();

	Если РассчитыватьХеши Тогда
		ЗаписьХешей.Закрыть();
	КонецЕсли;

	Возврат МассивФайловЧастей.Количество();

КонецФункции // СоздатьСписокФайлов()

#КонецОбласти // СлужебныеПроцедурыИФункции

Лог = ПараметрыСистемы.Лог();
