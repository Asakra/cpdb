// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

#Использовать "../../core"

Перем Лог;            // Объект                 - объект записи лога приложения

Функция ОбязательныеПараметрыЗаполнены(Знач Команда, ОписаниеОшибки = "") Экспорт

	НезаполненныеПараметры = Новый Массив();

	ПроверяемыеКоманды = Новый Массив();

	Для Каждого ТекКоманда Из Команда.КомандыРодители Цикл
		ПроверяемыеКоманды.Добавить(ТекКоманда);
	КонецЦикла;

	ПроверяемыеКоманды.Добавить(Команда);

	Для Каждого ТекКоманда Из ПроверяемыеКоманды Цикл

		НезаполненныеПараметрыКоманды = Новый Массив();

		СписокОпций = ТекКоманда.ПараметрыКоманды();

		ОбработанныеПараметры = Новый Соответствие();

		Для Каждого ТекЭлемент Из СписокОпций Цикл

			Опция = ТекКоманда.ОпцияИзИндекса(ТекЭлемент.Ключ);

			Если Опция = Неопределено
			 ИЛИ ОбработанныеПараметры.Получить(ТекЭлемент.Ключ) = Истина
			 ИЛИ НЕ Опция.ТребоватьУстановкиПользователем = Истина Тогда
				Продолжить;
			КонецЕсли;

			Значение = ТекКоманда.ЗначениеОпции(ТекЭлемент.Ключ);

			Если Не ЗначениеЗаполнено(Значение) Тогда
				Синонимы = Новый Массив();
				Для каждого ИмяСинонима Из Опция.НаименованияПараметров Цикл
					Синонимы.Добавить(ИмяСинонима);
					ОбработанныеПараметры.Вставить(ИмяСинонима, Истина);
				КонецЦикла;
				НезаполненныеПараметрыКоманды.Добавить(СтрШаблон("""%1""", СтрСоединить(Синонимы, " ")));
			КонецЕсли;

		КонецЦикла;

		Если ЗначениеЗаполнено(НезаполненныеПараметрыКоманды) Тогда
			НезаполненныеПараметры.Добавить(СтрШаблон("Для команды ""%1"" не заполнены параметры %2",
			                                          ТекКоманда.ПолучитьИмяКоманды(),
			                                          СтрСоединить(НезаполненныеПараметрыКоманды, ", ")));
		КонецЕсли;

	КонецЦикла;

	Если ЗначениеЗаполнено(НезаполненныеПараметрыКоманды) Тогда
		Лог.Ошибка(СтрСоединить(НезаполненныеПараметры, Символы.ПС));
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;

КонецФункции // ОбязательныеПараметрыЗаполнены()

Лог = ПараметрыСистемы.Лог();