// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

#Использовать "../../core"

Перем Лог;       // - Объект      - объект записи лога приложения

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("d sql-db", "", "имя базы для резервного копирования")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_SQL_DATABASE");
	
	Команда.Опция("p bak-path", "", "путь к файлу резервной копии")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_SQL_BACKUP_PATH");
	
	Команда.Опция("c create-db", Ложь, "создать базу в случае отсутствия")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_RESTORE_CREATE_DB");
	
	Команда.Опция("o db-owner", "", "имя владельца базы после восстановления")
	       .ТСтрока()
	       .ВОкружении("CPDB_SQL_RESTORE_DB_OWNER");
	
	Команда.Опция("cd compress-db", Ложь, "включить компрессию страниц таблиц и индексов после восстановления")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_RESTORE_COMPRESS_DB");
	
	Команда.Опция("sd shrink-db", Ложь, "сжать файлы данных после восстановления")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_RESTORE_SHRINK_DB");
	
	Команда.Опция("sl shrink-log", Ложь, "сжать файлы журнала транзакций после восстановления")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_RESTORE_SHRINK_LOG");
	
	Команда.Опция("pd db-path", , "путь к каталогу файлов данных базы после восстановления")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_SQL_RESTORE_DATA_PATH");
	
	Команда.Опция("pl db-logpath", , "путь к каталогу файлов журнала после восстановления")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_SQL_RESTORE_LOG_PATH");
	
	Команда.Опция("r db-recovery", "SIMPLE", "установить модель восстановления (RECOVERY MODEL),
	                                   |возможные значения ""FULL"", ""SIMPLE"",""BULK_LOGGED""")
	       .ТСтрока()
	       .ВОкружении("CPDB_SQL_RESTORE_RECOVERY_MODEL");
	
	Команда.Опция("cn db-changelfn", Ложь, "изменить логические имена файлов (LFN) базы, в соответствии с именем базы")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_RESTORE_CHANGE_LFN");
	
	Команда.Опция("ds delsrc", Ложь, "удалить файл резервной копии после восстановления")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_RESTORE_DELSRC");
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВыводОтладочнойИнформации = Команда.ЗначениеОпции("verbose");

	ПараметрыПриложения.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	Если НЕ ПараметрыПриложения.ОбязательныеПараметрыЗаполнены(Команда) Тогда
		Команда.ВывестиСправку();
		Возврат;
	КонецЕсли;

	ПараметрыПодключения = Новый Структура("Сервер, Пользователь, ПарольПользователя");
	ПараметрыПодключения.Сервер               = Команда.ЗначениеОпцииКомандыРодителя("sql-srvr");
	ПараметрыПодключения.Пользователь         = Команда.ЗначениеОпцииКомандыРодителя("sql-user");
	ПараметрыПодключения.ПарольПользователя   = Команда.ЗначениеОпцииКомандыРодителя("sql-pwd");

	База                 = Команда.ЗначениеОпции("sql-db");
	ПутьКРезервнойКопии  = Команда.ЗначениеОпции("bak-path");
	СоздаватьБазу        = Команда.ЗначениеОпции("create-db");
	ВладелецБазы         = Команда.ЗначениеОпции("db-owner");
	ВключитьКомпрессию   = Команда.ЗначениеОпции("compress-db");
	СжатьБазу            = Команда.ЗначениеОпции("shrink-db");
	СжатьФайлЛог         = Команда.ЗначениеОпции("shrink-log");
	ПутьКФайлуДанных     = Команда.ЗначениеОпции("db-path");
	ПутьКФайлуЖурнала    = Команда.ЗначениеОпции("db-logpath");
	МодельВосстановления = Команда.ЗначениеОпции("db-recovery");
	ИзменитьЛИФ          = Команда.ЗначениеОпции("db-changelfn");
	УдалитьИсточник      = Команда.ЗначениеОпции("delsrc");

	ПодключениеКСУБД = Новый ПодключениеКСУБД(ПараметрыПодключения.Сервер,
	                                          ПараметрыПодключения.Пользователь,
	                                          ПараметрыПодключения.ПарольПользователя);
	
	РаботаССУБД = Новый РаботаССУБД(ПодключениеКСУБД);

	РаботаССУБД.ВыполнитьВосстановление(База,
	                                    ПутьКРезервнойКопии,
	                                    ПутьКФайлуДанных,
	                                    ПутьКФайлуЖурнала,
	                                    СоздаватьБазу);

	Если УдалитьИсточник Тогда
		РаботаССУБД.УдалитьИсточник(ПутьКРезервнойКопии);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВладелецБазы) Тогда
		РаботаССУБД.ИзменитьВладельца(База, ВладелецБазы);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МодельВосстановления) Тогда
		РаботаССУБД.ИзменитьМодельВосстановления(База, МодельВосстановления);
	КонецЕсли;
	
	Если ИзменитьЛИФ Тогда
		РаботаССУБД.ИзменитьЛогическиеИменаФайлов(База);
	КонецЕсли;
	
	Если ВключитьКомпрессию Тогда
		РаботаССУБД.ВключитьКомпрессию(База);
	КонецЕсли;
	
	Если СжатьБазу Тогда
		РаботаССУБД.СжатьБазу(База);
	КонецЕсли;
	
	Если СжатьФайлЛог Тогда
		РаботаССУБД.СжатьФайлЛог(База);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта()

	Лог = ПараметрыПриложения.Лог();

КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий
