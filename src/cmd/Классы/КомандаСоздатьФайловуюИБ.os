// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

#Использовать "../../core"

Перем Лог;       // - Объект      - объект записи лога приложения

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("ip ib-path", "", "путь к каталогу базы 1С")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_IB_REF");
	
	Команда.Опция("ee err-if-exist", "", "сообщить об ошибке если по указанному пути существует ИБ 1С")
	       .Флаговый()
	       .ВОкружении("CPDB_IB_ERROR_IF_EXIST");
	
	Команда.Опция("nl name-in-list", "", "имя в списке баз пользователя (если не задано, то ИБ в список не добавляется)")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_NAME_IN_LIST");
	
	Команда.Опция("tp tmplt-path", "", "путь к шаблону для создания информационной базы (*.cf; *.dt).
	                                   | Если шаблон не указан, то будет создана пустая ИБ")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_TEMPLATE_PATH");
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВыводОтладочнойИнформации = Команда.ЗначениеОпции("verbose");

	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	Если НЕ ПараметрыПриложения.ОбязательныеПараметрыЗаполнены(Команда) Тогда
		Команда.ВывестиСправку();
		Возврат;
	КонецЕсли;

	ПутьКБазе            = Команда.ЗначениеОпции("ib-path");
	ИмяВСпискеБаз        = Команда.ЗначениеОпции("name-in-list");
	ПутьКШаблону         = Команда.ЗначениеОпции("tmplt-path");
	ОшибкаЕслиСуществует = Команда.ЗначениеОпции("err-if-exist");
	
	ИспользуемаяВерсияПлатформы = Команда.ЗначениеОпцииКомандыРодителя("v8version");
	
	РаботаСИБ.СоздатьФайловуюБазу(ПутьКБазе,
	                              ИспользуемаяВерсияПлатформы,
	                              ОшибкаЕслиСуществует,
	                              ПутьКШаблону,
	                              ИмяВСпискеБаз);

КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта()

	Лог = ПараметрыСистемы.Лог();

КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий
