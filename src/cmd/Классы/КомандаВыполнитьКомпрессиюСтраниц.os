// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

Перем Лог;       // - Объект      - объект записи лога приложения

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("d sql-db", "", "имя базы для резервного копирования")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_SQL_DATABASE");
	
	Команда.Опция("sd shrink-db", Ложь, "сжать файлы данных")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_SHRINK_DB");
	
	Команда.Опция("sl shrink-log", Ложь, "сжать файлы журнала транзакций")
	       .Флаговый()
	       .ВОкружении("CPDB_SQL_SHRINK_LOG");
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВыводОтладочнойИнформации = Команда.ЗначениеОпции("verbose");

	ПараметрыПриложения.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	Сервер             = Команда.ЗначениеОпцииКомандыРодителя("sql-srvr");
	Пользователь       = Команда.ЗначениеОпцииКомандыРодителя("sql-user");
	ПарольПользователя = Команда.ЗначениеОпцииКомандыРодителя("sql-pwd");
	База               = Команда.ЗначениеОпции("sql-db");
	СжатьБазу          = Команда.ЗначениеОпции("shrink-db");
	СжатьФайлЛог       = Команда.ЗначениеОпции("shrink-log");

	ПодключениеКСУБД = Новый ИнструментыСУБД(Сервер, Пользователь, ПарольПользователя);

	ВключитьКомпрессию(ПодключениеКСУБД, База);
	
	Если СжатьБазу Тогда
		СжатьБазу(ПодключениеКСУБД, База);
	КонецЕсли;

	Если СжатьФайлЛог Тогда
		СжатьФайлЛог(ПодключениеКСУБД, База);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Включает компрессию данных базы на уровне страниц
//   
// Параметры:
//   ПодключениеКСУБД    - ИнструментыСУБД    - объект подключения к СУБД
//   База                - Строка             - имя базы
//
Процедура ВключитьКомпрессию(ПодключениеКСУБД, База) Экспорт

	Лог.Информация("Начало компрессии страниц базы ""%1""", База);
		
	Попытка
		Результат = ПодключениеКСУБД.ВключитьКомпрессиюСтраниц(База);

		Если НЕ Результат Тогда
			ТекстОшибки = СтрШаблон("Ошибка включения компрессии страниц в базе ""%1""", База);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;

		Лог.Информация("Включена компрессия страниц в базе ""%1""", База);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка включения компрессии страниц в базе ""%1"": ""%2""",
		                        База,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

КонецПроцедуры // ВключитьКомпрессию()

// Выполняет сжатие базы (shrink)
//   
// Параметры:
//   ПодключениеКСУБД    - ИнструментыСУБД    - объект подключения к СУБД
//   База                - Строка             - имя базы
//
Процедура СжатьБазу(ПодключениеКСУБД, База) Экспорт

	Лог.Информация("Начало сжатия (shrink) базы ""%1""", База);
		
	Попытка
		Результат = ПодключениеКСУБД.СжатьБазу(База);

		Если НЕ Результат Тогда
			ТекстОшибки = СтрШаблон("Ошибка сжатия базы ""%1""", База);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;

		Лог.Информация("Выполнено сжатие базы ""%1""", База);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка сжатия базы ""%1"": ""%2""",
		                        База,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

КонецПроцедуры // СжатьБазу()

// Выполняет сжатие файла лога (shrink)
//   
// Параметры:
//   ПодключениеКСУБД    - ИнструментыСУБД    - объект подключения к СУБД
//   База                - Строка             - имя базы
//
Процедура СжатьФайлЛог(ПодключениеКСУБД, База) Экспорт

	Лог.Информация("Начало сжатия (shrink) файла ""%1_log""", База);
		
	Попытка
		Результат = ПодключениеКСУБД.СжатьФайлЛог(База);

		Если НЕ Результат Тогда
			ТекстОшибки = СтрШаблон("Ошибка сжатия файла ""%1_log""", База);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;

		Лог.Информация("Выполнено сжатие файла ""%1_log""", База);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка сжатия файла ""%1_log"": ""%2""",
		                        База,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

КонецПроцедуры // СжатьФайлЛог()

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта()

	Лог = ПараметрыПриложения.Лог();

КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий
