// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

#Использовать "../../core"

Перем Лог;       // - Объект      - объект записи лога приложения

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("s service", "", "адрес сервиса NextCloud")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_NC_SERVICE");

	Команда.Опция("u user", "", "имя пользователя сервиса NextCloud")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_NC_USER");

	Команда.Опция("n pwd", "", "пароль пользователя сервиса NextCloud")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_NC_PWD");

	Команда.Опция("p path", "", "Путь к локальному каталогу для сохранения загруженных файлов")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_NC_GET_PATH");
	
	Команда.Опция("f file", "", "путь к файлу на NextCloud для загрузки")
	       .ТСтрока()
	       .ВОкружении("CPDB_NC_GET_FILE");
	
	Команда.Опция("l list", "", "путь к файлу на NextCloud со списком файлов,
	                            |которые будут загружены (параметр -file игнорируется)")
	       .ТСтрока()
	       .ВОкружении("CPDB_NC_GET_LIST");
	
	Команда.Опция("ds delsrc", "", "удалить исходные файлы после получения")
	       .Флаговый()
	       .ВОкружении("CPDB_NC_GET_DEL_SRC");
	
   КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВыводОтладочнойИнформации = Команда.ЗначениеОпции("verbose");

	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	Если НЕ ПараметрыПриложения.ОбязательныеПараметрыЗаполнены(Команда) Тогда
		Команда.ВывестиСправку();
		Возврат;
	КонецЕсли;

	ЭтоСписокФайлов = Истина;
	
	АдресСервиса       = Команда.ЗначениеОпции("service");
	ИмяПользователя    = Команда.ЗначениеОпции("user");
	ПарольПользователя = Команда.ЗначениеОпции("pwd");
	ЦелевойПуть        = Команда.ЗначениеОпции("path");

	ПутьНаДиске        = Команда.ЗначениеОпции("list");
	Если НЕ ЗначениеЗаполнено(ПутьНаДиске) Тогда
		ПутьНаДиске = Команда.ЗначениеОпции("file");
		ЭтоСписокФайлов	= Ложь;
	КонецЕсли;

	УдалитьИсточник  = Команда.ЗначениеОпции("delsrc");

	Если ПустаяСтрока(ПутьНаДиске) Тогда
		ВызватьИсключение  "Не задан путь к файлу для получения из NextCloud";
	КонецЕсли;

	Сервис = Новый ПодключениеNextCloud(АдресСервиса, ИмяПользователя, ПарольПользователя);

	ПутьКСкачанномуФайлу = РаботаСФайлами.ПолучитьФайлИзNextCloud(Сервис, ПутьНаДиске, ЦелевойПуть, УдалитьИсточник);

	ФайлИнфо = Новый Файл(ПутьКСкачанномуФайлу);

	КаталогНаДиске = СтрЗаменить(ПутьНаДиске, ФайлИнфо.Имя, "");

	Если ЭтоСписокФайлов Тогда
		МассивПолучаемыхФайлов = РаботаСФайлами.ПрочитатьСписокФайлов(ПутьКСкачанномуФайлу);
		Для Каждого ПолучаемыйФайл Из МассивПолучаемыхФайлов Цикл
			РаботаСФайлами.ПолучитьФайлИзNextCloud(Сервис,
			                                       ОбъединитьПути(КаталогНаДиске, ПолучаемыйФайл),
			                                       ОбъединитьПути(ЦелевойПуть, ПолучаемыйФайл),
			                                       УдалитьИсточник);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта()

	Лог = ПараметрыСистемы.Лог();

КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий
