// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

Перем Лог;       // - Объект      - объект записи лога приложения

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("C ib-path ibconnection", "", "строка подключения к ИБ")
	       .ТСтрока()
	       .Обязательный()
	       .ВОкружении("CPDB_IB_CONNECTION");
	
	Команда.Опция("U ib-user", "", "пользователь ИБ")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_USER");
	
	Команда.Опция("P ib-pwd", "", "пароль пользователя ИБ")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_PWD");
	
	Команда.Опция("e extention", "", "имя расширения, отключаемого от хранилища")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_EXTENTION");
	
	Команда.Опция("uc uccode", "", "ключ разрешения запуска ИБ")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_UC_CODE");
	
	Команда.Опция("vv v8version", "", "маска версии платформы 1С")
	       .ТСтрока()
	       .ВОкружении("CPDB_IB_V8VERSION");

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВыводОтладочнойИнформации = Команда.ЗначениеОпции("verbose");

	ПараметрыПриложения.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	ПараметрыИБ        = Новый Структура();

	ПараметрыИБ.Вставить("СтрокаПодключения", Команда.ЗначениеОпции("ib-path"));
	ПараметрыИБ.Вставить("Пользователь",      Команда.ЗначениеОпции("ib-user"));
	ПараметрыИБ.Вставить("Пароль",            Команда.ЗначениеОпции("ib-pwd"));

	ИмяРасширения               = Команда.ЗначениеОпции("extension");
	ИспользуемаяВерсияПлатформы = Команда.ЗначениеОпции("v8version");
	КлючРазрешения              = Команда.ЗначениеОпции("uccode");

	Попытка
		ОтключитьОтХранилища(ПараметрыИБ,
		                     ИспользуемаяВерсияПлатформы,
		                     ИмяРасширения,
		                     КлючРазрешения);
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка отключения базы %1 от хранилища: %2%3",
		                        ПараметрыИБ.СтрокаПодключения,
		                        Символы.ПС,
		                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Процедура ОтключитьОтХранилища(ПараметрыИБ,
                               Знач ИспользуемаяВерсияПлатформы,
                               Знач ИмяРасширения = "",
                               Знач КлючРазрешения = "")

	Конфигуратор = ЗапускПриложений.НастроитьКонфигуратор(ПараметрыИБ.СтрокаПодключения,
                                                          ПараметрыИБ.Пользователь,
                                                          ПараметрыИБ.Пароль,
                                                          ИспользуемаяВерсияПлатформы);

	Если НЕ ПустаяСтрока(КлючРазрешения) Тогда
		Конфигуратор.УстановитьКлючРазрешенияЗапуска(КлючРазрешения);
	КонецЕсли;

	МенеджерХранилища = Новый МенеджерХранилищаКонфигурации(, Конфигуратор);

	Если ЗначениеЗаполнено(ИмяРасширения) Тогда
		МенеджерХранилища.УстановитьРасширениеХранилища(ИмяРасширения);
	КонецЕсли;

	МенеджерХранилища.ОтключитьсяОтХранилища();

	Лог.Информация("Информационная база %1 отключена от хранилища.",
	               ПараметрыИБ.СтрокаПодключения);

КонецПроцедуры // ОтключитьОтХранилища()

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта()

	Лог = ПараметрыПриложения.Лог();

КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий
